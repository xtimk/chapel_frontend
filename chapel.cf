entrypoints Program ;

position token POpenGraph ('{');
position token PCloseGraph ('}');

position token POpenParenthesis ('(');
position token PCloseParenthesis (')');

--position token POpenBracket ('[');
--position token PCloseBracket (']');

position token PSemicolon (';');
-- position token PComma (',');

position token PIdent (letter (letter|digit|'_'|'\'')*) ;
position token PString ('"' ((char - ["\"\\"]) | ('\\' ["\"\\nt"]))* '"') ;
position token PChar ('\'' ((char - ["'\\"]) | ('\\' ["'\\nt"])) '\'') ;
position token PDouble (digit+ '.' digit+ ('e' '-'? digit+)?) ;
position token PInteger (digit+) ;


Progr.  Program ::= Module ;

Mod.    Module ::= [Ext] ;

-- [].       [Ext] ::= ;
(:[]).    [Ext] ::= Ext;
(:).      [Ext] ::= Ext [Ext];

ExtDecl.   Ext    ::= Declaration ;
ExtFun.    Ext    ::= Function ;

NoAssgmDec.     Declaration ::= Type PIdent PSemicolon;
AssgmDec.       Declaration ::= Type PIdent AssgnmOp Exp PSemicolon;


FunDec.       Function ::= Signature Body ;

Sign.            Signature ::= Type PIdent FunctionParams ;

FunParams.      FunctionParams ::= POpenParenthesis [Param] PCloseParenthesis ;

[].       [Param] ::= ;
(:[]).    [Param] ::= Param;
(:).      [Param] ::= Param "," [Param];

ParNoMode.   Param  ::= Type PIdent ;
ParWMode.    Param  ::= Mode Type PIdent ;


FunBlock.       Body ::= POpenGraph [BodyStatement] PCloseGraph ;

[].       [BodyStatement] ::= ;
(:).      [BodyStatement] ::= BodyStatement [BodyStatement];

Stm.       BodyStatement  ::= Statement ;
Fun.       BodyStatement  ::= Function PSemicolon ;
Decl.      BodyStatement  ::= Declaration ;
Block.     BodyStatement  ::= Body ;

DoWhile.   Statement      ::= "do" "while" Body Guard ;
StExp.     Statement      ::= Exp PSemicolon;

SGuard.    Guard          ::= POpenParenthesis Exp PCloseParenthesis ;     

Tint.      Type ::= "int" ;
-- Tid.       Id   ::= PIdent ;

AssgnEq.   AssgnmOp ::= PAssignmEq ;
AssgnPlEq.   AssgnmOp ::= PAssignmPlus ;
position token PAssignmEq ('=') ;
position token PAssignmPlus ('+''=') ;

RefMode.   Mode ::= "ref" ;


EAss.        Exp ::= Exp AssgnmOp Exp4;
Elor.        Exp4  ::= Exp4 "||" Exp5;
Eland.       Exp5  ::= Exp5 "&&" Exp6;
Ebitor.      Exp6  ::= Exp6 "|" Exp7;
Ebitexor.    Exp7  ::= Exp7 "^" Exp8;
Ebitand.     Exp8  ::= Exp8 "&" Exp9;
Eeq.         Exp9  ::= Exp9 "==" Exp10;
Eneq.        Exp9  ::= Exp9 "!=" Exp10;
Elthen.      Exp10 ::= Exp10 "<" Exp11;
Egrthen.     Exp10 ::= Exp10 ">" Exp11;
Ele.         Exp10 ::= Exp10 "<=" Exp11;
Ege.         Exp10 ::= Exp10 ">=" Exp11;
Eleft.       Exp11 ::= Exp11 "<<" Exp12;
Eright.      Exp11 ::= Exp11 ">>" Exp12;
Eplus.       Exp12 ::= Exp12 "+" Exp13;
Eminus.      Exp12 ::= Exp12 "-" Exp13;
Etimes.      Exp13 ::= Exp13 "*" Exp14;
Ediv.        Exp13 ::= Exp13 "/" Exp14;
Emod.        Exp13 ::= Exp13 "%" Exp14;

_. Exp  ::= Exp4 ;
_. Exp4  ::= Exp5 ;
_. Exp5  ::= Exp6 ;
_. Exp6  ::= Exp7 ;
_. Exp7  ::= Exp8 ;
_. Exp8  ::= Exp9 ;
_. Exp9  ::= Exp10 ;
_. Exp10 ::= Exp11 ;
_. Exp11 ::= Exp12 ;
_. Exp12 ::= Exp13 ;
_. Exp13 ::= Exp14 ;
InnerExp. Exp14 ::= POpenParenthesis Exp PCloseParenthesis ;


Evar.        Exp14 ::= PIdent;
Econst.      Exp14 ::= Constant;
Estring.     Exp14 ::= PString;

Efloat.        Constant ::= PDouble;
Echar.         Constant ::= PChar;
Eint.          Constant ::= PInteger;